(ridersonthestorm
   (name "Riders on the Storm")
   (artist "The Doors")
   (master TRUE) 
   (song_id 1010022)  ; set to 0, unless you are Heather!
   (song
     (name        "songs/ridersonthestorm/ridersonthestorm")
     (tracks
        ((drum (0 1))
         (bass 2)
         (guitar (3 4))
         (vocals (5 6))
         (keys (7 8))
        )
     )
     (vocal_parts 1)    ; OPTIONAL but REQUIRED for vocal harmony
     ;                 DRUMS      BS     GUITAR     VOCALS      KEYS       TRACKS   
     (pans        (  -1.0  1.0   0.0  -1.0  1.0  -1.0  1.0  -1.0  1.0   -1.0  1.0 ))
     (vols        (  -3.2 -3.2  -2.5  -3.5 -3.5  -3.0 -3.0  -3.5 -3.5   -6.5 -6.5 ))
     (cores       (   -1   -1    -1     1    1    -1   -1    -1   -1     -1   -1  ))
    (drum_solo
        (seqs (kick.cue snare.cue tom1.cue tom2.cue crash.cue))
     )
     (drum_freestyle
        (seqs (kick.cue snare.cue hat.cue ride.cue crash.cue))
     )
   )
   (bank sfx/tambourine_bank.milo)
   (anim_tempo kTempoMedium)  
   (song_scroll_speed 2300)
   (preview 38000 66000)
   (song_length 403462)             
   (rank
      (drum 170)
      (guitar 180)
      (bass 170)
      (vocals 165)
      (keys 408)
      (real_guitar 000)
      (real_bass 000)
      (real_keys 442)
      (band 195)
   )
   (solo (guitar keys))  
   (format 10)
   (version 30)
   (game_origin rb3_dlc)
   (rating 1)     ; 1 = everyone, 2 = teen, 3 = mature, 4 = unrated
   (genre classicrock) 
   (vocal_gender male)
   (short_version 153828)    ; OPTIONAL - if set to 0, there is no short version of the song (milliseconds)
   (year_released 1971)
   (album_art TRUE)            ; OPTIONAL - defaults to false if it does not exist
   (album_name "L.A. Woman")  ; OPTIONAL
   (album_track_number 10)            ; OPTIONAL - cannot exist without album_name
   (vocal_tonic_note 4)           ; REQUIRED - VOX team - set this to correct note!
   (song_tonality 1)           ; REQUIRED - VOX team - 0 = major, 1 = minorfor all RB3 songs
)


